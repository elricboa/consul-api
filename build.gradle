apply plugin: "java"
apply plugin: "idea"
apply plugin: 'maven'

repositories {
	mavenCentral()
}

dependencies {
	compile "com.google.code.gson:gson:2.3"

	compile "org.apache.httpcomponents:httpcore:4.4.5"
	compile "org.apache.httpcomponents:httpclient:4.5.2"

	testCompile "junit:junit:4.11"
}

// --------------------------------------------------------------
// Tasks for publishing into Maven Central

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

def doUploadArchives = project.hasProperty('mavenReleaseUsername') && project.hasProperty('mavenReleasePassword')
if (doUploadArchives) {
	group = "com.ecwid.consul"
	archivesBaseName = "consul-api"
	version = "1.2.1-musically-mod-0.1"

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: mavenReleaseUrl) {
					authentication(userName: mavenReleaseUsername, password: mavenReleasePassword)
				}

				snapshotRepository(url: mavenSnapshotUrl) {
					authentication(userName: mavenSnapshotUsername, password: mavenSnapshotPassword)
				}

				pom.project {
					name 'Ecwid Consul API (Musical.ly Modification)'
					packaging 'jar'
					description 'Java client for Consul HTTP API (http://consul.io)'
					url 'https://github.com/yegong/consul-api'

					scm {
						connection 'scm:git:git@github.com:yegong/consul-api.git'
						developerConnection 'scm:git:git@github.com:yegong/consul-api.git'
						url 'https://github.com/yegong/consul-api.git'
					}

					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id 'vgv'
							name 'Vasily Vasilkov'
							email 'vgv@ecwid.com'
						}
					}
				}
			}
		}
	}
}

// --------------------------------------------------------------
// Wrapper

task wrapper(type: Wrapper) {
	gradleVersion = '2.14.1'
}
